#include <Arduino.h>
#include <Wire.h>
#include <WiFi.h>
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>

#include "wlan.h"
#include "config.h"
#include "website.h"
#include "api.h"
#include "display.h"
#include "nfc.h"
#include "scale.h"
#include "esp_task_wdt.h"
#include "commonFS.h"
#include "lang.h"

bool mainTaskWasPaused = 0;
uint8_t scaleTareCounter = 0;
bool touchSensorConnected = false;
bool booting = true;

// ##### SETUP #####
void setup() {
  Serial.begin(115200);

  uint64_t chipid;

  chipid = ESP.getEfuseMac(); //The chip ID is essentially its MAC address(length: 6 bytes).
  Serial.printf("ESP32 Chip ID = %04X", (uint16_t)(chipid >> 32)); //print High 2 bytes
  Serial.printf("%08X\n", (uint32_t)chipid); //print Low 4bytes.

  // Initialize SPIFFS
  initializeFileSystem();

  // Load language setting from NVS (before display init)
  loadLanguage();

  // Start Display
  setupDisplay();

  // WiFiManager
  initWiFi();

  // Webserver
  setupWebserver(server);

  // FilaMan API
  initFilaman();

  // NFC Reader
  startNfc();

  // Touch Sensor
  pinMode(TTP223_PIN, INPUT_PULLUP);
  if (digitalRead(TTP223_PIN) == LOW)
  {
    Serial.println("Touch Sensor is connected");
    touchSensorConnected = true;
  }

  // Scale
  start_scale(touchSensorConnected);
  if (scaleConnected){
    scaleTareRequest = true;
  }

  // WDT initialisieren mit 10 Sekunden Timeout
  bool panic = true; // Wenn true, löst ein WDT-Timeout einen System-Panik aus
  esp_task_wdt_init(10, panic);

  booting = false;
  oledShowProgressBar(6, NUM_SETUP_STEPS, DISPLAY_BOOT_TEXT, tr(STR_INIT_DONE));

  // Aktuellen Task (loopTask) zum Watchdog hinzufügen
  esp_task_wdt_add(NULL);
}


/**
 * Sicherer Intervall-Check, der millis() Überlauf behandelt
 * @param currentTime Aktueller millis() Wert
 * @param lastTime Zuletzt erfasste Zeit
 * @param interval Gewünschtes Intervall in Millisekunden
 * @return True, wenn das Intervall verstrichen ist
 */
bool intervalElapsed(unsigned long currentTime, unsigned long &lastTime, unsigned long interval) {
  if (currentTime - lastTime >= interval || currentTime < lastTime) {
    lastTime = currentTime;
    return true;
  }
  return false;
}

unsigned long lastWeightReadTime = 0;
const unsigned long weightReadInterval = 1000; // 1 second

unsigned long lastFilamanHeartbeatTime = 0;
unsigned long lastWifiCheckTime = 0;
unsigned long lastTopRowUpdateTime = 0;

uint8_t weightSend = 0;
int16_t lastWeight = 0;

// Variablen zur Tasten-Entprellung
unsigned long lastButtonPress = 0;
const unsigned long debounceDelay = 500; // 500 ms Entprellungs-Verzögerung

unsigned long lastConnErrorShowTime = 0;
const unsigned long connErrorShowInterval = 10000; // Verbindungsfehler alle 10 Sekunden anzeigen

// ##### PROGRAM START #####
void loop() {
  unsigned long currentMillis = millis();

  // Verbindungsfehler behandeln (nicht registriert oder nicht verbunden)
  if (intervalElapsed(currentMillis, lastConnErrorShowTime, connErrorShowInterval)) {
      if (!filamanRegistered && oledCanUpdate(DISPLAY_PRIORITY_WARNING)) {
          oledShowConnectionError(tr(STR_NOT_REGISTERED), WiFi.localIP().toString());
          oledSetPriority(DISPLAY_PRIORITY_WARNING, 3000);
          mainTaskWasPaused = true;
      } else if (!filamanConnected && oledCanUpdate(DISPLAY_PRIORITY_WARNING)) {
          oledShowConnectionError(tr(STR_API_CONN_LOST), WiFi.localIP().toString());
          oledSetPriority(DISPLAY_PRIORITY_WARNING, 3000);
          mainTaskWasPaused = true;
      }
  }

  // Überprüfe den Status des Touch Sensors (nur wenn Waage vorhanden)
  if (scaleConnected && touchSensorConnected && digitalRead(TTP223_PIN) == HIGH && currentMillis - lastButtonPress > debounceDelay)
  {
    lastButtonPress = currentMillis;
    scaleTareRequest = true;
    oledResetActivityTimer(); // Wake display on touch press
  }

  // Überprüfe regelmäßig die WLAN-Verbindung
  if (intervalElapsed(currentMillis, lastWifiCheckTime, WIFI_CHECK_INTERVAL))
  {
    checkWiFiConnection();
  }

  // Regelmäßiges Display-Update
  if (intervalElapsed(currentMillis, lastTopRowUpdateTime, DISPLAY_UPDATE_INTERVAL))
  {
    oledShowTopRow();
    oledCheckSleep(); // Put display to sleep if timeout elapsed
  }

  // WebSocket Cleanup alle 5 Sekunden (häufiger als vorher)
  // Tote Clients können WiFi-Ressourcen blockieren
  static unsigned long lastWsCleanup = 0;
  if (currentMillis - lastWsCleanup >= 5000) {
    ws.cleanupClients();
    lastWsCleanup = currentMillis;
  }

  // Regelmäßiger Heartbeat
  if (intervalElapsed(currentMillis, lastFilamanHeartbeatTime, FILAMAN_HEARTBEAT_INTERVAL))
  {
    sendHeartbeatAsync();
  }

  // Wenn die Waage nicht kalibriert ist, nur eine Warnung anzeigen
  if (scaleConnected && !scaleCalibrated)
  {
    // Warnung nicht anzeigen, wenn der Kalibrierungsprozess läuft
    if(!scaleCalibrationActive){
      oledDisplayText(tr(STR_SCALE_NOT_CALIBRATED));
      vTaskDelay(pdMS_TO_TICKS(1000));
    }
  }

  if (scaleConnected && scaleCalibrated)
  {
    // Ausgabe der Waage auf Display
    // Gewichtsanzeige blockieren, wenn wichtigere Display-Meldungen aktiv sind
    if(pauseMainTask == 0 && oledCanUpdate(DISPLAY_PRIORITY_STATUS))
    {
      int16_t displayWeight = getFilteredDisplayWeight();
      if (mainTaskWasPaused || (weight != lastWeight && (nfcReaderState == NFC_IDLE || tagProcessed)))
      {
        oledShowWeight((abs(displayWeight) < 2) ? 0 : displayWeight);
        oledSetPriority(DISPLAY_PRIORITY_STATUS, 0);  // Weight can always be overwritten
      }
      mainTaskWasPaused = false;
    }
    else
    {
      mainTaskWasPaused = true;
    }


    // Wenn Timer abgelaufen ist
    if (currentMillis - lastWeightReadTime >= weightReadInterval)
    {
      lastWeightReadTime = currentMillis;

      // Prüfen ob das Gewicht gleich bleibt und dann senden
      if (abs(weight - lastWeight) <= 2 && weight > 5)
      {
        weightCounterToApi++;
                // Feedback für stabiles Gewicht anzeigen, wenn der Sende-Schwellenwert erreicht wird
        if (weightCounterToApi == 3 && nfcReaderState == NFC_READ_SUCCESS && !tagProcessed && weightSend == 0) {
          oledShowProgressBar(2, 4, tr(STR_SPOOL_TAG), tr(STR_WEIGHT_STABLE));
          oledSetPriority(DISPLAY_PRIORITY_INFO, 1000);
        }
      }
      else
      {
                // Visuelles Feedback anzeigen, wenn ein Tag erkannt wurde und das Gewicht instabil ist
        if (weightCounterToApi > 0 && nfcReaderState == NFC_READ_SUCCESS && !tagProcessed && weightSend == 0) {
          oledShowProgressBar(1, 4, tr(STR_SPOOL_TAG), tr(STR_WEIGHING));
          oledSetPriority(DISPLAY_PRIORITY_INFO, 1000);
        }
        weightCounterToApi = 0;
        weightSend = 0;
      }

    lastWeight = weight;

    // Wenn ein Tag erkannt wurde und das Gewicht stabil ist (4+ seconds), an Bambuddy senden
    if (weightCounterToApi > 3 && weightSend == 0 && nfcReaderState == NFC_READ_SUCCESS && tagProcessed == false)
    {
      tagProcessed = true;

      // Bambuddy: Wir übergeben die erkannte UID, das Gewicht und ob es ein Bambu-Tag ist.
      syncBambuddySpoolAsync(activeTagUuid, weight, isBambuTag);
      Serial.println("Weight queued for Bambuddy API");

      weightSend = 1;

      // Feedback an den Benutzer
      oledShowProgressBar(3, 4, tr(STR_SPOOL_TAG), tr(STR_SENDING));
      oledSetPriority(DISPLAY_PRIORITY_ACTION, 2000);
    }
  }
  }
  esp_task_wdt_reset();
}
